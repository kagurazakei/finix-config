# files/directories starting with _(by default) and empty files will be ignored. Idea stolen from github.com/vic/import-tree
# supports multiple directory
#
# [] []?: -> []
let
  inputs = import ../../.tack;
  lib = import "${inputs.nixpkgs}/lib";
  inherit (builtins)
    filter
    any
    readFile
    readDir
    stringLength
    pathExists
    concatLists
    concatMap
    ;
  inherit (lib)
    mapAttrsToList
    hasSuffix
    hasPrefix
    filterAttrs
    ;

  recursiveImport =
    {
      dirs,
      excludePrefixedWith ? [ "_" ],
    }:
    let
      importDir =
        dir:
        let
          filteredAttrs =
            readDir dir
            |> (filterAttrs (
              n: v:
              !(any (prefix: hasPrefix prefix n) excludePrefixedWith) && (v == "directory" || hasSuffix ".nix" n)
            ));
          fn =
            i: type:
            if type == "directory" then
              importDir (dir + "/${i}")
            else if type == "regular" then
              [ (dir + "/${i}") ]
            else
              [ ];
        in
        concatLists (mapAttrsToList fn filteredAttrs)
        |> (filter (e: (pathExists e) && (stringLength (readFile e)) > 0));
    in
    concatMap importDir dirs;
in
recursiveImport
